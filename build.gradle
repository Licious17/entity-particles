buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

plugins {
    id "maven"
    id "org.jetbrains.kotlin.jvm" version "1.2.51"
    id "org.jetbrains.kotlin.kapt" version "1.2.51"
    id "com.github.johnrengelman.shadow" version "2.0.4"
    id "flavor.pie.promptsign" version "1.0.2" apply false
}

def isJitpack = System.getenv("JITPACK") != null

if (!isJitpack) {
    apply plugin: "flavor.pie.promptsign"
}

apply plugin: "net.minecraftforge.gradle.forge"

group "de.randombyte"
version "3.1.1"

minecraft {
    version = "1.12.2-14.23.5.2768"
    runDir = "run"
    mappings = "snapshot_20171003"
}

repositories {
    jcenter()
    maven { url "https://repo.spongepowered.org/maven/" }
    maven { url "https://jitpack.io" }
    maven { url "http://repo.bstats.org/content/repositories/releases" }
}

configurations {
    compile.extendsFrom shadow
    compile.extendsFrom kapt
}

dependencies {
    shadow "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.51"
    kapt "org.spongepowered:spongeapi:7.0.0"
    shadow("com.github.randombyte-developer.kosp:kosp:v2.2.3") { transitive = false }
    shadow "org.bstats:bstats-sponge:1.2"
    compile files("libs/Pixelmon-1.12.2-7.0.3.jar")
}

shadowJar {
    configurations = [project.configurations.shadow]

    relocate "kotlin", "de.randombyte.entityparticles.shaded.kotlin"
    relocate "de.randombyte.kosp", "de.randombyte.entityparticles.shaded.kosp"
    relocate "org.bstats", "de.randombyte.entityparticles.shaded.bstats"

    classifier = null // Remove "-all" suffix from output file name
}
build.dependsOn shadowJar
reobfJar.dependsOn shadowJar
install.dependsOn shadowJar
if (!isJitpack) {
    signArchives.dependsOn shadowJar
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
